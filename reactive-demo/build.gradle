plugins {
	id 'org.springframework.boot' version '2.2.0.BUILD-SNAPSHOT'
	id 'java'
	id 'jacoco'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.prady.sample'
version = '0.0.1'

sourceCompatibility = '11'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	
	implementation 'org.springframework.security:spring-security-crypto:5.1.5.RELEASE'
	
	implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
	implementation 'org.mapstruct:mapstruct:1.3.0.Final'
	implementation 'org.mapstruct:mapstruct-jdk8:1.3.0.Final'
	implementation 'org.slf4j:slf4j-api'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.0.Final'
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		exclude group: 'junit', module: 'junit'
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.95
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

jacocoTestCoverageVerification.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
